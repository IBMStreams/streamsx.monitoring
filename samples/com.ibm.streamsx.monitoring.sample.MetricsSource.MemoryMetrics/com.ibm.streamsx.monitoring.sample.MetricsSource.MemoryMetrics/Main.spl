//
// ****************************************************************************
// * Copyright (C) 2016, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

/**
 * This sample application monitors all jobs of all instances of the configured
 * domain and emits all PE memory metrics as tuple stream. The application uses
 * a subset of the output attributes only.
 * 
 */

namespace com.ibm.streamsx.monitoring.sample.MetricsSource.MemoryMetrics;

use com.ibm.streamsx.monitoring.metrics::MetricsSource;
use com.ibm.streamsx.monitoring.metrics::Notification;

composite Main {
	type
		MemoryMetric = tuple<
			rstring metricName,
			int64 metricValue,
			rstring domainId,
			rstring instanceId,
			int64 jobId,
			rstring jobName,
			rstring resource,
			int64 peId,
			int64 lastTimeRetrieved
		>;
	graph
		/*
		 * The MetricsSource generates a tuple for each metric value change.
		 */
		stream<MemoryMetric> ChangeNotifications as O = MetricsSource() {
			param
				connectionURL: getSubmissionTimeValue("connectionURL");
				user: getSubmissionTimeValue("user");
				password: getSubmissionTimeValue("password");
				domainId: getSubmissionTimeValue("domainId");
				scanPeriod: (float64)getSubmissionTimeValue("scanPeriod", "5.0");
				filterDocument: getThisToolkitDir()+"/etc" + "/MetricsSource_MonitorMemoryMetrics.json";
				emitMetricTuple: onChangedValue;
		}
		
		/*
		 * The Custom traces the received metric value change notifications.
		 */
		() as ChangeNotificationTracer = Custom(ChangeNotifications as I) {
			logic
			onTuple I: {
				appTrc(Trace.error, "Notification[change]: " + (rstring)I);
			}
			onPunct I: {
				appTrc(Trace.error, "Notification[change]: " + (rstring)currentPunct());
			}
		}
}
